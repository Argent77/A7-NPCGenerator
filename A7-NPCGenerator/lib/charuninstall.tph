// Macro: Registers data of currently installed characters in the following variables:
// $char_ref: indexed array containing CRE resref entries (numeric "char_ref" variable contains total number of entries)
// $char_name: associative array containing CHR filespec => character name entries.
// $char_gender: associative array containing CHR filespec => character gender entries.
// $char_class: associative array containing CHR filespec => character class entries.
// $char_kit: associative array containing CHR filespec => character kit entries.
// $char_level: associative array containing CHR filespec => character level entries.
DEFINE_ACTION_MACRO GET_INSTALLED_CHARS
BEGIN
  LOCAL_SET npcs = 0
  LOCAL_SET row = 0
  LOCAL_SPRINT cre_resref ~~
  LOCAL_SPRINT name ~~
  LOCAL_SPRINT gender_desc ~~
  LOCAL_SPRINT class_desc ~~
  LOCAL_SPRINT kit_desc ~~
  LOCAL_SPRINT level_desc ~~

  ACTION_IF (NOT VARIABLE_IS_SET ~char_ref~) BEGIN
    OUTER_SET char_ref = 0
  END

  COPY_EXISTING ~a7-npc.2da~ ~override~
    READ_2DA_ENTRIES_NOW npcs 1
    FOR (row = 3; row < npcs; ++row) BEGIN
      READ_2DA_ENTRY_FORMER npcs row 1 cre_resref
      TO_UPPER ~cre_resref~
      PATCH_IF (NOT VARIABLE_IS_SET $char_name(~%cre_resref%~) && FILE_EXISTS_IN_GAME ~%cre_resref%.CRE~) BEGIN
        INNER_ACTION BEGIN
          COPY_EXISTING ~%cre_resref%.CRE~ ~override~
            LPF GET_CRE_INFO RET name gender_desc class_desc kit_desc level_desc END
          BUT_ONLY
        END
        TEXT_SPRINT $char_ref(~%char_ref%~) ~%cre_resref%~
        TEXT_SPRINT $char_name(~%cre_resref%~) ~%name%~
        TEXT_SPRINT $char_gender(~%cre_resref%~) ~%gender_desc%~
        TEXT_SPRINT $char_class(~%cre_resref%~) ~%class_desc%~
        TEXT_SPRINT $char_kit(~%cre_resref%~) ~%kit_desc%~
        TEXT_SPRINT $char_level(~%cre_resref%~) ~%level_desc%~
        SET char_ref += 1
      END
    END
  BUT_ONLY IF_EXISTS
END


// Removes script blocks from the specified script file matching the search string.
DEFINE_ACTION_FUNCTION REMOVE_SCRIPT_BLOCK
INT_VAR
  case_sensitive  = 0 // whether to match case-sensitive
  use_regexp      = 0 // whether to match with regular expression
  count           = 0 // indicates how many matches should be removed (0=all available)
STR_VAR
  bcs_resref  = ~~    // the BCS resref
  search      = ~~    // the search string
RET
  matches             // returns number of removed script blocks
BEGIN
  OUTER_SET matches = 0
  ACTION_IF (NOT ~%bcs_resref%~ STR_EQ ~~ && NOT ~%search%~ STR_EQ ~~) BEGIN
    COPY_EXISTING ~%bcs_resref%.BCS~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        SET pos1 = 0
        WHILE (pos1 >= 0) BEGIN
          PATCH_IF (case_sensitive && use_regexp) BEGIN
            SET pos1 = INDEX_BUFFER(CASE_SENSITIVE EVALUATE_REGEXP ~%search%~)
          END ELSE PATCH_IF (case_sensitive && NOT use_regexp) BEGIN
            SET pos1 = INDEX_BUFFER(CASE_SENSITIVE EXACT_MATCH ~%search%~)
          END ELSE PATCH_IF (NOT case_sensitive && use_regexp) BEGIN
            SET pos1 = INDEX_BUFFER(CASE_INSENSITIVE EVALUATE_REGEXP ~%search%~)
          END ELSE BEGIN
            SET pos1 = INDEX_BUFFER(CASE_INSENSITIVE EXACT_MATCH ~%search%~)
          END
          PATCH_IF (pos1 >= 0) BEGIN
            SET pos1 = RINDEX_BUFFER(~^IF$~ pos1)
            PATCH_IF (pos1 >= 0) BEGIN
              SET pos2 = INDEX_BUFFER(~^END$~ pos1)
              PATCH_IF (pos2 > pos1) BEGIN
                SET pos2 += 3
                DELETE_BYTES pos1 (pos2 - pos1)
                SET matches += 1
                PATCH_IF (count > 0 && matches >= count) BEGIN
                  SET pos1 = "-1"
                END
              END
            END
          END
        END
      END
    BUT_ONLY IF_EXISTS
  END
END


// Removes script blocks for selected CRE from area scripts.
DEFINE_ACTION_FUNCTION REMOVE_CRE_SCRIPT
STR_VAR
  cre_resref = ~~
RET
  success
BEGIN
  OUTER_SET success = 0
  ACTION_IF (NOT ~%cre_resref%~ STR_EQ ~~ && FILE_EXISTS_IN_GAME ~%cre_resref%.CRE~) BEGIN
    // getting list of areas
    ACTION_CLEAR_ARRAY ~areas~
    COPY_EXISTING ~a7-npc.2da~ ~override~
      READ_2DA_ENTRIES_NOW npcs 1
      FOR (row = 3; row < npcs; ++row) BEGIN
        READ_2DA_ENTRY_FORMER npcs row 1 cre
        PATCH_IF (~%cre%~ STR_EQ ~%cre_resref%~) BEGIN
          READ_2DA_ENTRY_FORMER npcs row 3 are
          TO_UPPER ~are~
          SET $areas(~%are%~) = 1
        END
      END
    BUT_ONLY IF_EXISTS

    // removing blocks from area scripts
    ACTION_PHP_EACH areas AS are_resref => _ BEGIN
      LAF GET_AREA_SCRIPT STR_VAR are_resref RET are_script END
      LAF REMOVE_SCRIPT_BLOCK
        INT_VAR
          case_sensitive = 0
          use_regexp = 0
        STR_VAR
          bcs_resref = EVAL ~%are_script%~
          search = EVAL ~Global("A7CHR-%cre_resref%-CREATED","GLOBAL",0)~
        RET
          matches
      END
    END

    // removing blocks from creature script
    LAF REMOVE_SCRIPT_BLOCK
      INT_VAR
        case_sensitive = 0
        use_regexp = 0
      STR_VAR
        bcs_resref = ~A7CHRNPC~
        search = EVAL ~Name("A7CHR-%cre_resref%",Myself)~
      RET
        matches
    END
    
  END
END


// Removes entries for selected CRE from a7-npc.2da.
DEFINE_ACTION_FUNCTION REMOVE_CRE_ENTRIES
STR_VAR
  cre_resref = ~~
RET
  success
BEGIN
  OUTER_SET success = 0
  ACTION_IF (NOT ~%cre_resref%~ STR_EQ ~~ && FILE_EXISTS_IN_GAME ~%cre_resref%.CRE~) BEGIN
    COPY_EXISTING ~a7-npc.2da~ ~override~
      READ_2DA_ENTRIES_NOW npcs 1
      FOR (row = npcs - 1; row >= 3; --row) BEGIN
        READ_2DA_ENTRY_FORMER npcs row 1 cre
        PATCH_IF (~%cre%~ STR_EQ ~%cre_resref%~) BEGIN
          REMOVE_2DA_ROW row 1
        END
      END
    BUT_ONLY IF_EXISTS
  END
END


// Macro: Uninstalls all characters selected in the character selection menu.
// Requires the following variables to be initialized: $char_ref, $char_selected, $char_name
DEFINE_ACTION_MACRO UNINSTALL_CHARACTERS
BEGIN
  // Quick sanity check
  ACTION_IF (NOT VARIABLE_IS_SET ~char_selected~) BEGIN
    OUTER_SET char_selected = 0
  END
  ACTION_IF (NOT VARIABLE_IS_SET ~supported_games~) BEGIN
    OUTER_SET supported_games = 0
  END

  ACTION_IF (char_selected > 0) BEGIN
    OUTER_FOR (char_idx = 0; char_idx < char_ref; ++char_idx) BEGIN
      ACTION_IF (VARIABLE_IS_SET $char_selected(~%char_idx%~)) BEGIN
        OUTER_TEXT_SPRINT cre_resref $char_ref(~%char_idx%~)
        OUTER_TEXT_SPRINT name $char_name(~%cre_resref%~)
        OUTER_SPRINT msg @63  // Uninstalling character:
        PRINT ~%msg% %name% (%cre_resref%.cre)~

        // removing CRE script blocks in area scripts
        LAF REMOVE_CRE_SCRIPT STR_VAR cre_resref END

        // removing character entries from a7-npc.2da
        LAF REMOVE_CRE_ENTRIES STR_VAR cre_resref END

        // removing CRE resource
        ACTION_IF (FILE_EXISTS ~override/%cre_resref%.cre~) BEGIN
          DELETE ~override/%cre_resref%.cre~
        END
      END
    END
  END
END
